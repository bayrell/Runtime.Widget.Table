/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2023 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Widget.Table;

use Runtime.BaseObject;
use Runtime.Callback;
use Runtime.Web.BaseWidget;
use Runtime.Widget.ButtonModel;
use Runtime.Widget.RowButtons;
use Runtime.Widget.RowButtonsModel;
use Runtime.Widget.Styles;
use Runtime.Widget.Crud.CrudApiStorage;
use Runtime.Widget.Table.Table;


class TableModel extends BaseWidget
{
	CrudApiStorage storage = null;
	string component = classof Table;
	string widget_name = "table";
	Vector<Map> fields = [];
	RowButtonsModel top_buttons = new RowButtonsModel{
		"styles": ["@widget_table__top_buttons"]
	};
	Styles styles = new Styles{ "prefix": "widget_table" };
	
	
	/**
	 * Constructor
	 */
	void constructor(Dict params = null)
	{
		parent(params);
		if (params)
		{
			if (params.has("styles")) this.styles.addStyles(params.get("styles"));
		}
		
		this.setDataStorage( this.createDataStorage() );
		this.initWidgets();
		this.initFields();
		this.initTopButtons();
		this.initStyles();
	}
	
	
	/**
	 * Create data storage
	 */
	BaseObject createDataStorage() => null;
	
	
	/**
	 * Init table widgets
	 */
	void initWidgets()
	{
	}
	
	
	/**
	 * Init fields
	 */
	void initFields()
	{
	}
	
	
	/**
	 * Init top buttons
	 */
	void initTopButtons()
	{
	}
	
	
	/**
	 * Init styles
	 */
	void initStyles()
	{
	}
	
	
	/**
	 * Set data provider
	 */
	void setDataStorage(CrudApiStorage storage)
	{
		this.storage = storage;
	}
	
	
	/**
	 * Add field
	 */
	Map addField(Map field)
	{
		this.fields.append(field);
		return field;
	}
	
	
	/**
	 * Add field row buttons
	 */
	Map addFieldRowButtons()
	{
		/* Row buttons */
		RowButtonsModel row_buttons = new RowButtonsModel();
		
		/* Edit button */
		ButtonModel edit_button = row_buttons.add(new ButtonModel{
			"content": "Edit",
			"styles": [
				"default",
				"small",
			],
		});
		edit_button.events.add("onClick", new Callback(this.getWidget("save_dialog"), "onClick"));
		
		/* Delete button */
		ButtonModel delete_button = row_buttons.add(new ButtonModel{
			"content": "Delete",
			"styles": [
				"danger",
				"small",
			],
		});
		delete_button.events.add("onClick", new Callback(this.getWidget("delete_dialog"), "onClick"));
		
		/* Add buttons */
		Map field = this.addField({
			"name": "row_buttons",
			"component": classof RowButtons,
			"model": row_buttons,
		});
		
		return field;
	}
	
	
	/**
	 * Returns item by row number
	 */
	Dict getItemByRowNumber(int row_number) => this.storage.items.get(row_number);
	
	
	/**
	 * Returns item value
	 */
	var getItemValue(Map item, string field_name) => item.get(field_name);
	
	
	/**
	 * Import data
	 */
	void importData(Map import_data)
	{
		parent(import_data);
		if (this.storage)
		{
			this.storage.importData(import_data.get("storage"));
		}
	}
	
	
	/**
	 * Export data
	 */
	Map exportData()
	{
		Map data = parent();
		if (this.storage)
		{
			data.set("storage", this.storage.exportData());
		}
		return data;
	}
	
	
	/**
	 * Load table data
	 */
	async void loadData()
	{
		await parent();
		await this.storage.doSearch();
	}
	
	
	/**
	 * Refresh table
	 */
	async void refresh()
	{
		await this.storage.doSearch();
	}
}
